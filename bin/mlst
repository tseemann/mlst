#!/usr/bin/env perl
use strict;
use File::Spec;
use Data::Dumper;
use List::MoreUtils qw(pairwise);
use File::Temp qw(tempfile);
use FindBin;
use lib "$FindBin::RealBin/../perl5";
use MLST::PubMLST;
use MLST::Scheme;
use MLST::Logger qw(err msg);
use MLST::Requirements qw(require_exe);
use MLST::FileUtils qw(is_genbank genbank_to_fasta is_gzipped gunzip_file);

#..............................................................................
# Globals

my $VERSION = "2.0";
my $SEP = '/';
my $OUTSEP = "\t";

#..............................................................................
# Command line options

my(@Options, $debug, $quiet, $blastdb, $datadir, $threads,
             $list, $longlist, $scheme, $minid, $csv, $nopath);
setOptions();

#..............................................................................
# Option parsing

MLST::Logger->quiet($quiet);

$OUTSEP = ',' if $csv;  # default is tab

my $pubmlst = MLST::PubMLST->new( dir => $datadir );
my %scheme = (map { ($_->name => $_) } @{$pubmlst->schemes});

# in case user is forcing a scheme on us (legacy mode)
$scheme && ! exists $scheme{$scheme} and err("Invalid --scheme '$scheme'. Check using --list");

if ($list or $longlist) {
  if ($list) { 
    print join( " ", $pubmlst->names ), "\n";
  }
  elsif ($longlist) {
    for my $s (@{ $pubmlst->schemes }) {
      print join( $OUTSEP, $s->name, @{$s->genes} ), "\n";
    }
  }
  exit(0);
}

@ARGV or err("Please provide some FASTA files to determine the MLST of");

require_exe("blastn");

#..............................................................................
# Output results

# output the header for the old style syntax 
if ($scheme) {
  print join($OUTSEP, qw(FILE SCHEME ST), @{ $scheme{$scheme}->genes } ),"\n";
}

for my $argv (@ARGV) 
{
  my @temp;
  my $infile = $argv;
  
  if ( is_gzipped($argv) ) {
    (undef,$infile) = tempfile(UNLINK=>1);
    msg("Decompressing: $argv'");
    gunzip_file($argv, $infile);
    push @temp, $infile;
  }
  if ( is_genbank($infile) ) {
    my $orig = $infile;
    (undef,$infile) = tempfile(UNLINK=>1);
    msg("Converting to FASTA: $argv");
    genbank_to_fasta($orig, $infile);
    push @temp, $infile;
  }

  msg("Scanning: $argv [$infile]");  

  my($sch, $ST, $sig) = find_mlst($infile);
  $scheme && $sch ne $scheme and err("BUG: got back $sch despite --scheme $scheme");
  
  (undef,undef,$argv) = File::Spec->splitpath($argv) if $nopath;
  my @code = $sch eq '-' ? () : split m{$SEP}, $sig ;
  
  if ($scheme) {
    # old style "one scheme" layout
    print join($OUTSEP, $argv, $sch, $ST, @code),"\n";
  } 
  else {
    # new style "autodetect" layout
    my @gene = $sch eq '-' ? () : @{ $scheme{$sch}->genes };
    my @allele = pairwise { "$a($b)" } @gene, @code;
    print join($OUTSEP, $argv, $sch, $ST, @allele),"\n";
  }
  
  if (@temp) {
    msg("Deleting temporary files: @temp");
    unlink @temp;
  }
}

#----------------------------------------------------------------------

sub find_mlst {
  my($fname) = @_;
  my $cmd = "blastn -query \Q$fname\E -db \Q$blastdb\E -num_threads $threads"
           ." -ungapped -dust no -evalue 1E-20 -word_size 32 -max_target_seqs 10000 -culling_limit 2"
           ." -perc_identity $minid -outfmt '6 sseqid slen length nident'";
  msg("Running: $cmd") if $debug;

  my @hit = qx($cmd);
  # FIXME: we should /sort/ the hits here in case logic below is dodgy?
  
  my %res;
  my $res_count=0;
  foreach (@hit) {
    next unless m/ ^ (\w+)\.(\w+)[_-](\d+) \t (\d+) \t (\d+) \t (\d+) $ /x;
    my($sch, $gene, $num, $hlen, $alen, $nident) = ($1, $2, $3, $4, $5, $6);
    $res_count++;
    msg("[$res_count] $sch $gene $num | id=$nident/$alen | cov=$alen/$hlen") if $debug;
#    next unless $nident/$hlen > $minid/100 ;
    if ($scheme and $sch ne $scheme) {
      msg("Skipping $sch.$gene.$num allele as user specified --scheme $scheme");
      next;
    }
    if ($hlen == $alen and $nident == $hlen) {   # need full length 100% hits
      if (exists $res{$sch}{$gene} and $res{$sch}{$gene} !~ m/~/) {
        msg("WARNING: found addtional exact allele match $sch.$gene-$num");
        $res{$sch}{$gene} .= ",$num";
      }
      else {
        $res{$sch}{$gene} = "$num";
      }
    }
    else {
      my $label = ($alen == $hlen) ? "~$num" : "${num}?";
      $res{$sch}{$gene} ||= $label;
    }
  }

  # find the signature with the fewest missing/approximate alleles  
  my @sig = ( [ ($scheme || '-'), '-', join("/", ('-')x7) ] );  # sentinel
  for my $name (keys %res) {
    my $sig = $scheme{$name}->signature_of($res{$name});
    my $ST = $scheme{$name}->sequence_type($sig);
    push @sig, [ $name, $ST, $sig ];
  }
  @sig = sort { 
    ($a->[2] =~ tr/-~/-~/) <=> ($b->[2] =~ tr/-~/-~/)    # choose less missings
    or $a->[1] <=> $b->[1]   # if same, prefer scheme with 'older' number
  } @sig;
  print STDERR Dumper(@sig) if $debug;
  # take the top scorer
  return @{ $sig[0] };
}

#----------------------------------------------------------------------

sub show_version {
  my(undef,undef,$exe) = File::Spec->splitpath($0);
  print "$exe $VERSION\n";
  exit(0);
}

#----------------------------------------------------------------------
# Option setting routines

sub setOptions {
  use Getopt::Long;

  @Options = (
    {OPT=>"help",    VAR=>\&usage,             DESC=>"This help"},
    {OPT=>"debug!",  VAR=>\$debug, DEFAULT=>0, DESC=>"Verbose debug output to stderr"},
    {OPT=>"version!",  VAR=>\&show_version, DESC=>"Print version and exit"},
    {OPT=>"quiet!",  VAR=>\$quiet, DEFAULT=>0, DESC=>"Quiet - no stderr output"},
    {OPT=>"blastdb=s",  VAR=>\$blastdb, DEFAULT=>"$FindBin::RealBin/../db/blast/mlst.fa", DESC=>"BLAST database"},
    {OPT=>"datadir=s",  VAR=>\$datadir, DEFAULT=>"$FindBin::RealBin/../db/pubmlst", DESC=>"PubMLST data"},
    {OPT=>"scheme=s",  VAR=>\$scheme, DEFAULT=>'', DESC=>"Don't autodetect, force this scheme on all inputs"},
    {OPT=>"list!",  VAR=>\$list, DEFAULT=>0, DESC=>"List available MLST scheme names"},
    {OPT=>"longlist!",  VAR=>\$longlist, DEFAULT=>0, DESC=>"List allelles for all MLST schemes"},
    {OPT=>"minid=f",  VAR=>\$minid, DEFAULT=>95, DESC=>"DNA %identity to consider 'similar'"},
    {OPT=>"threads=i",  VAR=>\$threads, DEFAULT=>1, DESC=>"Number of BLAST threads (suggest GNU Parallel instead)"},
    {OPT=>"csv!",  VAR=>\$csv, DEFAULT=>0, DESC=>"Output CSV instead of TSV"},
    {OPT=>"nopath!",  VAR=>\$nopath, DEFAULT=>0, DESC=>"Strip filename paths from FILE column"},
  );

  &GetOptions(map {$_->{OPT}, $_->{VAR}} @Options) || usage();

  # Now setup default values.
  foreach (@Options) {
    if (defined($_->{DEFAULT}) && !defined(${$_->{VAR}})) {
      ${$_->{VAR}} = $_->{DEFAULT};
    }
  }
}

sub usage {
  print "Usage: $0 [options] <contigs.fa[.gz] | file.gbk[.gz]> ... \n";
  foreach (@Options) {
    printf "  --%-13s %s%s.\n",$_->{OPT},$_->{DESC},
           defined($_->{DEFAULT}) ? " (default '$_->{DEFAULT}')" : "";
  }
  exit(1);
}
 
#----------------------------------------------------------------------
